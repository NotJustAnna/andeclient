plugins {
    id 'java'
    id 'maven'
    id "maven-publish"
    id 'com.jfrog.bintray' version '1.8.4'
}

group = 'pw.aru.libs'
version = '1.0-SNAPSHOT'
sourceCompatibility = 11

repositories {
    jcenter()
}

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'org.immutables:value-annotations:2.7.5'
    annotationProcessor 'org.immutables:value:2.7.5'

    implementation 'pw.aru.libs:eventpipes:1.3.1'
    implementation 'com.sedmelluq:lavaplayer:1.3.11'
    implementation 'org.json:json:20180813'
    implementation 'org.slf4j:slf4j-api:1.7.26'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError = false
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addStringOption('-html5')
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId project.group.toString()
            artifactId project.name.toString()
            version project.version.toString()
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ['BintrayRelease']
    pkg {
        repo = 'maven'
        name = project.name.toString()
        licenses = ['MIT']
        vcsUrl = 'https://github.com/arubot/eventpipes.git'
        publish = true
        version {
            name = project.version
        }
    }
}

bintrayUpload {
    dependsOn build
    dependsOn 'publishBintrayReleasePublicationToMavenLocal'
}